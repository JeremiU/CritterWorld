plugins {
    id 'java'
    id 'eclipse'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.8'
}

apply plugin: 'java'
// external repositories where dependencies of your project are found
repositories {
    mavenCentral()
}

javafx {
    version = "19"
    modules = [ 'javafx.controls', 'javafx.web', 'javafx.fxml' ]
}

sourceSets {
    main {
        java {
            srcDirs 'src/main/java'
            srcDirs 'src/easyIO/src'
        }
    }
}

dependencies {
    // the spark webserver, excluding vulnerable groups
    implementation('com.sparkjava:spark-core:2.9.1') {
        exclude group: 'org.eclipse.jetty', module: 'jetty-http'
        exclude group: 'org.eclipse.jetty', module: 'jetty-server'
        exclude group: 'org.eclipse.jetty', module: 'jetty-webapp'
        exclude group: 'org.eclipse.jetty', module: 'jetty-xml'
    }
    // GSON, for parsing and generating JSON
    implementation 'com.google.code.gson:gson:2.8.9'
    // run tests with JUnit 5
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.5.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.5.2'
    // JavaFX dependencies (ensuring they match the version specified)
    implementation "org.openjfx:javafx-controls:19"
    implementation "org.openjfx:javafx-web:19"
    implementation "org.openjfx:javafx-fxml:19"
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        exceptionFormat "full"
    }
}

jar {
    archiveBaseName.set('Console')
    manifest {
        attributes 'Main-Class': 'console.Console'
    }
    from(sourceSets.main.output) {
        include '**'
    }
}

// parse and mutate jar for A4
tasks.register('parseAndMutateJar', Jar) {
    archiveBaseName.set('ParseAndMutate')
    manifest {
        attributes 'Main-Class': 'main.ParseAndMutateApp'
    }
    from(sourceSets.main.output) {
        include '**'
    }
}

tasks.register('runGUI', JavaExec) {
    mainClass = 'gui.DisplayController'
    classpath = sourceSets.main.runtimeClasspath
    args = [] // Add any necessary arguments here, if needed

    jvmArgs = [
            '--module-path', classpath.asPath,
            '--add-modules', 'javafx.controls,javafx.fxml,javafx.web',
            '--add-opens', 'java.desktop/sun.awt.X11=ALL-UNNAMED'
    ]
}

jar.dependsOn parseAndMutateJar
run.dependsOn runGUI